// vars
Перем АдресСервера;
Перем Токен;
Перем Соединение;
Перем Заголовок;

Функция ПолучитьДанныеИзОтвета(Ответ)
 	ОтветСтрокой = Ответ.ПолучитьТелоКакСтроку();
	
	Чтение = Новый ЧтениеJson();
	Чтение.УстановитьСтроку(ОтветСтрокой);
	
	Если Ответ.КодСостояния = 200 Тогда
		Данные = ПрочитатьJSON(Чтение, ложь);
	    Возврат Данные;
	ИначеЕсли Ответ.КодСостояния = 400 тогда
		Данные = ПрочитатьJSON(Чтение, ложь);
		ВызватьИсключение("Ошибка: " + Данные.message);
	Иначе
		ВызватьИсключение("HTTP ошибка: " + Ответ.КодСостояния);
	КонецЕсли;		
КонецФункции

Процедура Авторизация(ТокенКлиента) Экспорт
	Токен = ТокенКлиента;	
КонецПроцедуры

Процедура СменитьСервер(Адрес, ЗащищенноеСоединение) Экспорт
	АдресСервера = Адрес;
	Если ЗащищенноеСоединение Тогда
		Соединение = Новый HTTPСоединение(АдресСервера,,,,,, Новый ЗащищенноеСоединениеOpenSSL());
	Иначе
		Соединение = Новый HTTPСоединение(АдресСервера);
	КонецЕсли;
КонецПроцедуры

Функция СклонятьРусский(лемма) Экспорт
	ТекстЗапроса = "/russian/declension?s=" + лемма;
	Если Токен <> null Тогда
		ТекстЗапроса = ТекстЗапроса + "&token=" + Токен;
	КонецЕсли;
	
	Запрос = Новый HTTPЗапрос(ТекстЗапроса);	
	Запрос.Заголовки = Заголовок;
	
	Ответ = Соединение.Получить(Запрос);
	Данные = ПолучитьДанныеИзОтвета(Ответ);
	Возврат Данные;
	
КонецФункции

// default init
Токен = null;
АдресСервера = "ws3.morpher.ru";
Соединение = Новый HTTPСоединение(АдресСервера,,,,,, Новый ЗащищенноеСоединениеOpenSSL());

Заголовок = Новый Соответствие();
Заголовок.Вставить("Accept", "application/json");	